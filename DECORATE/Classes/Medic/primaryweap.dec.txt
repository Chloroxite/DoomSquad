Actor SMGSwitchingModes : Inventory
{
	Inventory.MaxAmount 1
}

Actor SMGMag : Ammo
{
	Inventory.MaxAmount 24
	+IGNORESKILL
}
Actor SMGAmmo : Ammo
{
	Inventory.RestrictedTo "MedicPlayer"
	Inventory.MaxAmount 200
	Ammo.BackpackAmount 10
	Ammo.BackpackMaxAmount 400
	+IGNORESKILL
}
Actor SMGHealerAmmo : Ammo
{
	Inventory.RestrictedTo "MedicPlayer"
	Inventory.MaxAmount 100
	+IGNORESKILL
}

Actor Badger : Weapon
{
	Obituary "%o was widdled away by %k's Badger."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 4
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "SMGMag"
	Weapon.SlotNumber 3
	Decal BulletChip
	Tag "Badger"
	Inventory.RestrictedTo "MedicPlayer"
	Inventory.PickupMessage "You somehow picked up a badger straight off the ground..."
	
	+WEAPON.AMMO_CHECKBOTH
	
	States
	{
		Select:
			TNT1 A 0 A_JumpIfInventory("SMGSwitchingModes", 1, "SelectMode")
			
			MP5R A 0 A_Raise
			MP5R A 1 Offset(240, 330) A_PlaySound("Weapons/SMGPK")
			MP5R A 1 Offset(220, 300)
			MP5R A 1 Offset(200, 290)
			MP5R A 1 Offset(180, 230)
			MP5R A 1 Offset(160, 180)
			MP5R A 1 Offset(140, 140)
			MP5R A 1 Offset(120, 110)
			MP5R A 1 Offset(70, 90)
			MP5K A 1 Offset(36, 30)
			MP5K A 1 Offset(20, 30)
			Goto Ready
		SelectMode:
			MP5K A 1 Offset(20, 30)
			MP5K A 1 Offset(36, 45)
			MP5R A 1 Offset(50, 90) A_PlaySound("Weapons/M4A1R2")
			MP5K A 1 Offset(36, 45)
			MP5K A 1 Offset(20, 30) A_TakeInventory("SMGSwitchingModes", 1)
			Goto Ready
		Deselect:
			TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
			TNT1 A 0 A_JumpIfInventory("SMGSwitchingModes", 1, "DeselectMode")
			
			MP5K A 1
			MP5K A 1 Offset(20, 0)
			MP5K A 1 Offset(36, 0)
			MP5R A 1 Offset(70, 90)
			MP5R A 1 Offset(120, 110)
			MP5R A 1 Offset(140, 140)
			MP5R A 1 Offset(160, 180)
			MP5R A 1 Offset(180, 230)
			MP5R A 1 Offset(200, 290)
			MP5R A 1 Offset(220, 300)
			MP5R A 1 Offset(240, 330)
			MP5R A 0 A_Lower
			Wait
		DeselectDeath:
			TNT1 A 0 A_TakeInventory("SMGSwitchingModes", 1)
			MP5K A 1 A_Lower
			Wait
		DeselectMode:
			MP5K A 0 A_Lower
			Wait
		Ready:
			//MP5K A 0 A_GiveInventory("SMGHealerAmmo", 1)
			MP5K A 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("SMGMag", 1, 1)
			Goto Reload
			
			MP5K A 1 A_TakeInventory("SMGMag", 1)
			MP5F A 1 bright A_FireBullets(5, 4, -1, 6, "BulletPuff")
			MP5K A 0 A_SetPitch(pitch-1)
			MP5F A 0 A_PlaySound("Weapons/SMGFIR", 1, 0.8)
			MP5F B 1
			MP5F B 0 A_ReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("BadgerHealer", 1, 1)
			Goto Ready
			
			TNT1 A 0 A_GiveInventory("SMGSwitchingModes", 1)
			TNT1 A 0 A_SelectWeapon("BadgerHealer")
			Goto Ready
		Reload:
			TNT1 A 0 A_JumpIfInventory("SMGMag", 0, "Reset")
			TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
			Goto Reset
			
			MP5K A 2
			MP5R A 2
			MP5R B 2 A_PlaySound("Weapons/SMGR1")
			MP5R C 2 
			MP5R D 2
			MP5R E 2 Offset(-32, 0)
			MP5R E 2 Offset(-40, 0)
			MP5R E 4
			MP5R F 2
			MP5R G 2 Offset(-40, 0) A_PlaySound("Weapons/SMGR2")
			MP5R G 2 Offset(-30, 0)
			MP5R G 2 Offset(-28, 0)
			MP5R G 2 Offset(-30, 0)
			MP5R G 2 Offset(-40, 0)
			MP5R H 2
			Goto ReloadLoop
		ReloadLoop:
			HORN A 0 A_JumpIfInventory("SMGMag", 0, "Ready")
		
			HORN A 0 A_JumpIfInventory("Clip", 1, 1)
			Goto Ready
		
			HORN A 0 A_GiveInventory("SMGMag", 1)
			HORN A 0 A_TakeInventory("Clip", 1)
			Loop
		Reset:
			TNT1 A 0
			Goto Ready
		Spawn:
			//This weapon sprite should never show up in world
			Stop 
	}
}

Actor BadgerHealer : Badger
{
	Obituary "HOW?"
	Weapon.AmmoType "SMGHealerAmmo"
	Weapon.SlotNumber ""
	states
	{
	Fire:
		TNT1 A 0 A_JumpIfInventory("SMGHealerAmmo", 50, 1)
		Goto Ready
		
		MP5K A 1 A_TakeInventory("SMGHealerAmmo", 50)
		MP5F A 1 bright A_FireCustomMissile("HealingBoolet")
		MP5F A 0 A_PlaySound("Weapons/SMGHL", 1, 0.5)
		MP5F B 1
		MP5K A 20
		Goto Ready
	AltFire:
		TNT1 A 0 A_GiveInventory("SMGSwitchingModes", 1)
		TNT1 A 0 A_SelectWeapon("Badger")
		Goto Ready
	}
}

Actor HealingOverTime : PowerupGiver
{
	 inventory.pickupmessage "You are being healed!"
	 inventory.icon "MEGAA0"
	 powerup.color lightblue 0.1
	 inventory.maxamount 0
	 inventory.usesound "pickups/slowmo"
	 powerup.type "Regeneration"
	 powerup.duration 150
	 translation "128:143=64:79"
	 +AUTOACTIVATE
	 +INVENTORY.FANCYPICKUPSOUND
}

Actor HealingBoolet : FastProjectile
{
	Damage 1
    Radius 1
    Height 1
    Speed 500
    Projectile
    Obituary "How the hell did you manage to die to this?"
    Renderstyle "Normal"
    Scale 0.5
    Decal DoomImpScorch
    +FORCERADIUSDMG
	+BLOODSPLATTER
    +NOTIMEFREEZE
	states
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		PUFF A 0 A_NoGravity
		PUFF A 0 A_PlaySound("Weapons/SMGHLIM")
		PUFF B 4 A_RadiusGive("HealingOverTime", 40, RGF_PLAYERS, 1)
		PUFF C 4
		PUFF D 4
		Stop
	}
}
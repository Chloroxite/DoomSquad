actor HornetMag : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 12
}

actor HEmptyMag : Inventory
{
	Inventory.MaxAmount 1
}

actor ngBulletPuff : BulletPuff
{
	+NOEXTREMEDEATH
}

actor Hornet : Weapon
{
	Obituary "%o was stung by %k's Hornet."
	Inventory.PickupMessage "There is literally no way this message should show up."
	Inventory.RestrictedTo "CommandoPlayer"
	Weapon.SlotNumber 1
	Weapon.Kickback 100
	Weapon.AmmoType "HornetMag"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2
	Tag "Hornet"
	Decal "BulletChip"
	+WEAPON.NOAUTOFIRE
	States
	{
	Ready:
		HORN A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		HORE A 1
		HORE B 1
		HORE C 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		HORN A 0 A_Raise
		HORE C 1 Offset(0, 30) A_PlaySound("Weapons/HORNPIK")
		HORE B 1
		HORE A 1
		Goto Ready
	Fire:
		"----" A 1 A_JumpIfInventory("HornetMag", 1, 1)
		Goto Reload
		
		HORF A 1 bright A_FireBullets (2,2,-1,15,"ngBulletPuff")
		HORF A 0 A_SpawnItemEx("SpentBullet",Cos(Pitch)*30,15,35-(Sin(Pitch)*25),Cos(Pitch)*FRandom(2,4),FRandom(5,6),-Sin(Pitch)*FRandom(3,6),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		HORF AAAA 0 A_FireCustomMissile("SmolSmonkSpawner", 0, 0, 12, 2)
		TNT1 A 0 A_FireCustomMissile("BulletTrail", FRANDOM(-1, 1), 0, 22, 0, 0, FRANDOM(-2, 2))
		HORF A 0 A_SetPitch(pitch-.5)
		HORF A 0 A_PlaySound("Weapons/HORNFIR", 1, 0.7)
		HORF A 0 A_TakeInventory("HornetMag", 1)
		HORF B 1 bright
		HORF CDE 1
		Goto Ready
	Reload:
		"----" A 1 A_JumpIfInventory("HornetMag", 0, "Reset")
		"----" A 1 A_JumpIfInventory("HornetMag", 1, 2)
		"----" A 1 A_GiveInventory("HEmptyMag", 1)
		
		HORR ABCDEFG 2
		HORR H 2 A_PlaySound("Weapons/HORNRL1", 0)
		HORR IJKLMN 2
		HORR O 2 
		HORR P 2
		HORR Q 2 
		HORA BCD 2
		HORR RST 2
		HORR T 1 A_JumpIfInventory("HEmptyMag", 1, 1)
		Goto Reload+39
		
		HORR UVWXYZ 2
		HORT A 2 A_PlaySound("Weapons/HORNPIK", 0)
		HORT BCDEF 2
		HORT A 0 ACS_NamedExecute("ReloadWeapon", 0, 0, 0, 0)
		Goto Reset
	Reset:
		HORN A 2 A_TakeInventory("HEmptyMag", 1)
		Goto Ready
	Flash:
		TNT1 A 2 bright
		Stop
	Spawn:
		//nofuckyou
		Stop
	}
}
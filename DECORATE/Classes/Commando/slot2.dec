Actor SMGMag : Ammo
{
	Inventory.MaxAmount 48
	+IGNORESKILL
}

Actor Badger : Weapon
{
	Obituary "%o was widdled down by %k's Badger."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 4
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "SMGMag"
	Weapon.SlotNumber 4
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2
	Decal BulletChip
	Tag "Badger"
	Inventory.RestrictedTo "CommandoPlayer"
	Inventory.PickupMessage "You somehow picked up a badger straight off the ground..."
	
	+WEAPON.AMMO_CHECKBOTH
	
	States
	{
		Select:
			MP5R A 0 A_Raise
			P90H C 1 Offset(1, 40) A_PlaySound("Weapons/SMGPK")
			P90H BA 1
			Goto Ready
		Deselect:
			TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
			
			P90K A 1
			P90H ABC 1
			P90K A 0 A_Lower
			Wait
		DeselectDeath:
			P90K A 1 A_Lower
			Wait
		Ready:
			P90K A 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("SMGMag", 1, 1)
			Goto Reload
			
			P90K A 0 A_TakeInventory("SMGMag", 1)
			P90F A 1 bright A_FireBullets(5, 4, -1, 16, "BulletPuff", FBF_NORANDOM)
			TNT1 A 0 A_FireCustomMissile("BulletTrail", FRANDOM(-1.5, 1.5), 0, 0, 0, 0, FRANDOM(-2.5, 2.5))
			TNT1 A 0 A_SetPitch(pitch-1)
			TNT1 A 0 A_PlaySound("Weapons/RSMGF", 1)
			P90F B 1
			TNT1 B 0 A_ReFire
			Goto Ready
		Reload:
			TNT1 A 0 A_JumpIfInventory("SMGMag", 0, "Reset")
			TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
			Goto Reset
			
			P9R1 A 2
			P9R1 B 2
			P9R1 C 2
			P9R1 D 2
			P9R1 E 2
			P9R1 F 2
			P9R1 G 2
			P9R1 H 2
			P9R1 I 2 A_PlaySound("Weapons/SMGR1", 1)
			P9R1 J 2
			P9R1 K 2
			P9R1 L 2
			P9R1 M 2
			P9R1 N 2
			P9R1 O 2 A_PlaySound("Weapons/SMGR2", 1)
			P9R1 P 2
			P9R1 Q 2
			P9R1 R 2
			P9R1 S 2
			P9R1 T 2
			P9R1 U 2
			P9R1 V 2
			P9R1 W 2
			P9R1 X 2
			P9R1 Y 2
			P9R1 Z 2
			P9R2 A 2
			P9R2 B 2
			P9R2 C 2
			P9R2 D 2
			P9R2 E 2 A_PlaySound("Weapons/SMGR2", 1)
			P9R2 F 2
			P9R2 G 2 A_PlaySound("Weapons/SMGR1", 1)
			P9R2 H 2
			P9R2 K 2
			P9R2 L 2
			P9R2 M 2
			P9R2 N 2
			P9R2 O 2
			P9R2 P 2
			P9R2 Q 2
			P9R2 R 2
			Goto ReloadLoop
		ReloadLoop:
			HORN A 0 A_JumpIfInventory("SMGMag", 0, "Ready")
		
			HORN A 0 A_JumpIfInventory("Clip", 1, 1)
			Goto Ready
		
			HORN A 0 A_GiveInventory("SMGMag", 1)
			HORN A 0 A_TakeInventory("Clip", 1)
			Loop
		Reset:
			TNT1 A 0
			Goto Ready
		Spawn:
			//This weapon sprite should never show up in world
			Stop 
	}
}
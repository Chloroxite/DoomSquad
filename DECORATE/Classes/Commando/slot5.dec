Actor Lazerbeam
{
	RenderStyle Translucent
	Alpha 0.3
	Scale 0.1
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	states
	{
		Spawn:
			LAZR A 3 bright
		Stop
	}
}

Actor LazerImpact
{
	RenderStyle Translucent
	Alpha 0.8
	Scale 0.2
	+NOGRAVITY
	+DONTSPLASH
	+ALWAYSPUFF
	+FORCEXYBILLBOARD
	states
	{
		Spawn:
			TNT1 A 0 A_Jump(127, "Spawn2")
			LZRI A 3 bright
		Stop
		Spawn2:
			TNT1 A 0 A_Jump(127, "Spawn3")
			LZRI B 3 bright
		Stop
		Spawn3:
			LZRI C 3 bright
		Stop
	}
}

Actor MGOverheated : Inventory
{
	Inventory.MaxAmount 1
}

Actor MGHeat : Ammo
{
	Inventory.MaxAmount 100
	+IGNORESKILL
}

Actor Megawave : Weapon
{
	Obituary "%k nuked %o with a Megawave."
	Weapon.AmmoType "Cell"
	Weapon.AmmoType2 "MGHeat"
	Weapon.SlotNumber 5
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 2
	Decal BRIGHTMK
	AttackSound "null"
	Tag "MegaWave"
	Inventory.RestrictedTo "CommandoPlayer"
	Inventory.PickupMessage "I don't know how this got on the floor..."
	States
	{
		Select:
			MGAW A 0 A_PlaySound("Weapons/FOCUSPK", 1)
			TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
			Goto SelectEmpty
			
			MGAW A 0 A_Raise
			MGPK C 1  Offset(-1, 30)
			MGPK BA 1
			TNT1 A 0 A_TakeInventory("MGHeat", 100)
			TNT1 A 0 A_TakeInventory("MGOverheated", 1)
			Goto ready
		SelectEmpty:
			MGPE A 0 A_Raise
			MGPE C 1 Offset(-1, 30)
			MGPE BA 1
			TNT1 A 0 A_TakeInventory("MGHeat", 100)
			TNT1 A 0 A_TakeInventory("MGOverheated", 1)
			Goto ready
		Deselect:
			TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
			Goto SelectEmpty
			
			MGPK ABC 1
			TNT1 A 0 A_Lower
			Wait
		DeselectEmpty:
			MGPE ABC 1
			TNT1 A 0 A_Lower
			Wait
		Ready:
			TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
			Goto ReadyEmpty
			
			TNT1 A 0 A_TakeInventory("MGHeat", 4)
			MGAW A 1 A_WeaponReady
			MGAW B 1 A_WeaponReady
			Loop
		ReadyEmpty:
			TNT1 A 0 A_JumpIfInventory("MGOverheated", 1, 2)
			TNT1 A 0 A_JumpIfInventory("Cell", 1, "Ready")
			TNT1 A 0 A_JumpIfInventory("MGHeat", 1, 2)
			TNT1 A 0 A_TakeInventory("MGOverheated", 1)

			TNT1 A 0 A_TakeInventory("MGHeat", 1)
			TNT1 A 0 A_FireCustomMissile("SmonkSpawner", 0, 0, 10, 2)
			MGEM A 2 A_WeaponReady
			TNT1 A 0 A_TakeInventory("MGHeat", 1)
			TNT1 A 0 A_FireCustomMissile("SmonkSpawner", 0, 0, 10, 2)
			MGEM B 2 A_WeaponReady
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("MGOverheated", 1, "ReadyEmpty")
			TNT1 A 0 A_JumpIfInventory("MGHeat", 0, "Overheat")
			TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
			Goto Reset
			
			MGAF A 1 bright A_RailAttack(10, 5, 0, none, orange, RGF_SILENT||RGF_NOPIERCING, 0, "LazerImpact", 0, 0, 400, 1, 1, 0, "Lazerbeam", -3)
			TNT1 A 0 A_PlaySound("Weapons/FOCUSSTR", 0)
			TNT1 A 0 A_GiveInventory("MGHeat", 2)
			TNT1 A 0 A_TakeInventory("Cell", 1)
			MGAF B 1 bright A_PlaySound("Weapons/FOCUSLP", 1, 1, true)
			TNT1 A 0 A_Refire("FireCont")
			Goto FireEnd
		FireCont:
			TNT1 A 0 A_JumpIfInventory("MGHeat", 0, "Overheat")
			TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
			Goto FireEnd
			
			MGAF B 1 bright A_RailAttack(10, 5, 0, none, orange, RGF_SILENT||RGF_NOPIERCING, 0, "LazerImpact", 0, 0, 400, 1, 1, 0, "Lazerbeam", -3)
			TNT1 A 0 A_GiveInventory("MGHeat", 2)
			TNT1 A 0 A_TakeInventory("Cell", 1)
			MGAF C 1 bright
			TNT1 A 0 A_Refire("FireCont2")
			Goto FireEnd
		FireCont2:
			TNT1 A 0 A_JumpIfInventory("MGHeat", 0, "Overheat")
			TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
			Goto FireEnd
			
			MGAF D 1 bright A_RailAttack(10, 5, 0, none, orange, RGF_SILENT||RGF_NOPIERCING, 0, "LazerImpact", 0, 0, 400, 1, 1, 0, "Lazerbeam", -3)
			TNT1 A 0 A_GiveInventory("MGHeat", 2)
			TNT1 A 0 A_TakeInventory("Cell", 1)
			MGAF F 1 bright
			TNT1 A 0 A_Refire("FireCont")
			Goto FireEnd
		FireEnd:
			MGAF A 1 A_PlaySound("Weapons/FOCUSEND", 1)
			MGAW A 1
			Goto Reset
		Reset:
			TNT1 A 0 A_JumpIfInventory("MGHeat", 0, "Overheat")
			TNT1 A 0 A_JumpIfInventory("Cell", 1, 1)
			Goto ReadyEmpty
			
			TNT1 A 0 
			Goto Ready
		Overheat:
			TNT1 A 0 A_FireCustomMissile("SmonkSpawner", 0, 0, 12)
			TNT1 A 0 A_GiveInventory("MGOverheated", 1)
			MGOH A 1 A_PlaySound("Weapons/FOCUSOH", 1)
			MGOH BCDEFG 1
			MGEM A 1
			Goto ReadyEmpty
	}
}
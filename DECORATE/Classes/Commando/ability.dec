Actor LightningBeam
{
	RenderStyle Translucent
	Alpha 0.3
	Scale 0.1
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	states
	{
		Spawn:
			LIHT A 3 bright
		Stop
	}
}

Actor FlashBall
{
	Scale 0.15
	Speed 60
	+FORCEXYBILLBOARD
	+MISSILE
	+FORCEPAIN
	+BLOODLESSIMPACT
	states
	{
		Spawn:
			TNT1 A 0
		Fly:
			FLSH ABCDEFGH 1 bright
			Loop
		Death:
			FLSH I 1 bright A_PlaySound("Tac/FLASH", 2)
			FLSH JKL 1 bright
			TNT1 AAAAAAA 0 A_SpawnItemEx("SmonkSpawner", 0, 0, 5, 5)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Explode(1, 200, 0, TRUE)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Explode(1, 200, 0, TRUE)
			Stop
			
	}
}

Actor comHyper : Weapon
{
	Obituary "%o was zapped out of existance by %k."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 1
	Weapon.AmmoType "HypEnergy"
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2
	Weapon.BobRangeX 0.5
	Decal BRIGHTMK
	Tag "I'm 99% sure this shouldn't show up."
	Inventory.RestrictedTo "CommandoPlayer"
	States
	{
		Deselect:
			TNT1 A 0 A_TakeInventory("inHyper", 1)
			TNT1 A 0 A_TakeInventory("HypEnergy", 50)
			TNT1 A 0 A_PlaySound("Ult/CHExit", 0, 1)
			CHYF A 1 A_SetBlend("700 700 700", 1, 100)
			CHYF A 1 bright A_Lower
			Wait
		Select:
			TNT1 A 1 A_JumpIfInventory("inHyper", 1, "NotReady") //if you already have this, you've probably entered hypermode during map transition.
			
			CHYP AAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 1 A_GiveInventory("inHyper", 1)
			Goto Ready
		NotReady:
			TNT1 A 1 A_WeaponReady(WRF_NOFIRE)
			Loop
		Ready:
			CHYP A 2 bright A_PlaySound("Ult/CHEnter", 0, 1)
			CHYP A 2 bright 
			CHYP BCDEFGHIJKLMNOP 2 bright
			CHYP Q 2 bright A_SetBlend("700 700 700", 1, 50)
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("Spark1", 10, 0, FRANDOM(0, 360), 2, FRANDOM(0, 180))
			TNT1 A 0 A_Quake(10, 20, 0, 1)
			CHYP R 2
			CHYP S 2
			CHYP T 2
			CHYP U 2
			CHYP V 2
			CHYP W 2
			CHYP X 2
			CHYP Y 2
			CHYP Z 2
			Goto Ready2
		Ready2:
			CHYF A 8 bright A_WeaponReady//(WRF_NOSWITCH)
			TNT1 A 0 A_TakeInventory("HypEnergy", 1)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("HypEnergy", 1, 1)
			Goto Ready2
			
			CHYF BCD 1 bright
			CHYF E 1 bright
			TNT1 A 0 A_PlaySound("Ult/CHATK", 0, 1)
			TNT1 A 0 A_PlaySound("Ult/CHATKLP", 2, 1, TRUE)
			TNT1 AAAA 0 bright A_RailAttack(30, 5, 0, none, orange, RGF_SILENT, 10, "LazerImpact", 5, 5, 800, 2, 1, 0, "LightningBeam", -3)
			TNT1 A 0 A_TakeInventory("HypEnergy", 1)
			TNT1 AA 0 A_FireCustomMissile("Spark1", FRANDOM(-10, 10), 0, 10, 5, 0, FRANDOM(-10, 10))
			TNT1 A 0 A_Quake(10, 10, 0, 1)
			TNT1 A 0 A_Refire("FireCont")
			Goto FireEnd
		FireCont:
			TNT1 A 0 A_JumpIfInventory("HypEnergy", 1, 1)
			Goto FireEnd
			
			CHYF FGH 1 bright
			CHYF HHHH 0 bright A_RailAttack(30, 5, 0, none, orange, RGF_SILENT, 10, "LazerImpact", 5, 5, 800, 2, 1, 0, "LightningBeam", -3)
			TNT1 A 0 A_TakeInventory("HypEnergy", 1)
			TNT1 AA 0 A_FireCustomMissile("Spark1", FRANDOM(-10, 10), 0, 10, 5, 0, FRANDOM(-10, 10))
			TNT1 A 0 A_Quake(1, 8, 0, 1)
			TNT1 A 0 A_Refire("FireCont")
			Goto FireEnd
		FireEnd:
			CHYF D 1 bright A_StopSound(2)
			CHYF CB 1 bright
			Goto Ready2
		
	}
}

Actor ComAbility : Weapon
{
	Obituary "%o managed to die to a flashbang somehow..."
	Weapon.AmmoType "AbEnergy"
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2
	Weapon.BobRangeX 0.5
	Tag "I'm 99% sure this shouldn't show up."
	Inventory.RestrictedTo "CommandoPlayer"
	states
	{
		Select:
			TNT1 A 0 A_PlaySound("Tac/ABTHROW", 1)
			FTHR A 2 A_Raise
			TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Raise
			TNT1 A 0 
		Ready:
			FTHR ABCD 1 A_GiveInventory("inAbility", 1)
		Fire:
			FTHR E 1 A_FireCustomMissile("FlashBall", 0)
			TNT1 A 0 A_TakeInventory("AbEnergy", 100)
			FTHR FG 1
			Goto Idle
		Idle:
			TNT1 A 1 A_WeaponReady
			Loop
		Deselect:
			TNT1 A 0 A_TakeInventory("inAbility", 1)
			TNT1 A 1 A_Lower
			Wait
	}
}
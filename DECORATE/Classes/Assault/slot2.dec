Actor UACEmptyMag : Inventory
{
	Inventory.MaxAmount 1
}

Actor UACMag : Ammo
{
	Inventory.MaxAmount 30
	+IGNORESKILL
}

Actor UAC4A : Weapon
{
	Obituary "%o was turned into swiss cheese %k's UAC4A."
	Inventory.PickupMessage "You got the UAC4A assault rifle!"
	Weapon.SlotNumber 4
	Weapon.Kickback 100
	Weapon.AmmoType "Clip"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 4
	Weapon.AmmoType2 "UACMag"
	Weapon.AmmoGive 0
	Weapon.AmmoUse 0
	Inventory.RestrictedTo "AssaultPlayer"
	Tag "UAC4A"
	Decal "BulletChip"
	+AMMO_CHECKBOTH
	
	States
	{
	Ready:
		UAC4 A 24 A_WeaponReady(WRF_ALLOWRELOAD)
		UAC4 D 4 A_WeaponReady(WRF_ALLOWRELOAD)
		UAC4 E 48 A_WeaponReady(WRF_ALLOWRELOAD)
		UAC4 D 4 A_WeaponReady(WRF_ALLOWRELOAD)
		UAC4 A 24  A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
		TNT1 A 0 A_TakeInventory("UACEmptyMag", 1)
		
		UAC4 A 1 
		UAC4 A 1 Offset(20, 0)
		UAC4 A 1 Offset(36, 0)
		UAC4 B 1 Offset(70, 30)
		UAC4 B 1 Offset(120, 50)
		UAC4 B 0 A_Lower
		Wait
	DeselectDeath:
		UAC4 A 1 A_Lower
		TNT1 A 0 A_TakeInventory("UACEmptyMag", 1)
		Wait
	Select:
		UAC4 B 0 A_Raise
		UAC4 B 1 Offset(120, 50) A_PlaySound("Weapons/M4A1PK")
		UAC4 A 1 Offset(70, 30)
		UAC4 A 1 Offset(36, 30)
		UAC4 A 1 Offset(20, 30)
		Goto Ready
	Fire:
		UAC4 A 0 A_JumpIfInventory("UACMag", 1, 1)
		Goto Reload
		
		UAC4 A 0
		UAC4 A 1 bright A_FireBullets(2,2,1,18,"BulletPuff", FBF_NORANDOM)
		UAC4 A 0 A_SetPitch(pitch-.8)
		//randomize visual recoil
		TNT1 A 0 A_Jump(85, "FireRecoil1")
		TNT1 A 0 A_Jump(85, "FireRecoil2")
		
		UACF A 1 bright A_PlaySound("Weapons/M4A1FIR", 1, 0.8)
		UACF B 1 Radius_Quake (1,1,0,1,0)
		UACF G 1 //A_Print("Message1")
		UACF G 0 A_TakeInventory("UACMag", 1)
		UACF G 0 A_Refire
		Goto Ready
	FireRecoil1:
		UACF C 1 bright A_PlaySound("Weapons/M4A1FIR", 1, 0.8)
		UACF D 1 Radius_Quake (1,1,0,1,0)
		UACF G 1 //A_Print("Message2")
		UACF G 0 A_TakeInventory("UACMag", 1)
		UACF G 0 A_Refire
		Goto Ready
	FireRecoil2:
		UACF E 1 bright A_PlaySound("Weapons/M4A1FIR", 1, 0.8)
		UACF F 1 Radius_Quake (1,1,0,1,0)
		UACF G 1 //A_Print("Message3")
		UACF G 0 A_TakeInventory("UACMag", 1)
		UACF G 0 A_Refire
		Goto Ready
	Reload:
		UAC4 A 0 A_JumpIfInventory("UACMag", 0, "Reset")
		UAC4 A 0 A_JumpIfInventory("Clip", 1, 1)
		Goto Reset
		TNT1 A 0 A_JumpIfInventory("UACMag", 1, 2)
		TNT1 A 0 A_GiveInventory("UACEmptyMag", 1)
		
		UACR A 1
		UACE A 1
		UACE B 1
		UACE C 1
		UACE D 1
		UACR B 3
		UACR C 2
		UACR D 2
		UACR E 2
		UACR F 2
		UACR G 2
		UACR H 2
		UACR I 2
		UACR J 2
		UACR K 2
		UACR L 2 A_PlaySound("Weapons/M4A1R1", 1)
		UACR M 2
		UACR N 2
		UACR O 2
		UACR P 2
		UACR Q 2
		UACR R 2
		UACR S 2
		UACR T 2
		UACR U 3 A_PlaySound("Weapons/M4A1R3", 1)
		UACR V 2
		UACR W 2
		UACR X 2
		UACR Y 2
		UACR Z 2
		UACH A 1
		UACH B 1
		UACH C 1
		TNT1 A 0 ACS_NamedExecute("ReloadWeapon", 0, 0, 1, 1)
		Goto ReloadFinish
	ReloadFinish:
		TNT1 A 0 A_JumpIfInventory("UACEmptyMag", 1, 1)
		Goto Ready
		
		UACB ABCD 2 Offset(1, 40)
		UACB E 2 A_PlaySound("Weapons/M4A1BB")
		UACB FG 2
		UACB H 2 A_PlaySound("Weapons/M4A1BR")
		TNT1 A 0 A_TakeInventory("UACEmptyMag", 1)
		Goto Ready
	Reset:
		TNT1 A 0
		Goto Ready
	Flash:
		TNT1 A 2 bright
		Stop
	Spawn:
		//lol no
		Stop
	}
}
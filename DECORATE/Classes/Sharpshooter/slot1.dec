Actor MAGnum : Ammo
{
	Inventory.MaxAmount 6
}

actor M29Magnum : Weapon
{
	Obituary "%o was pistol whipped by %k's M29 Magnum."
	Inventory.PickupMessage "Ah yes, my best friends, smith and wesson."
	Inventory.RestrictedTo "SharpshooterPlayer"
	Weapon.SlotNumber 1
	Weapon.Kickback 1
	Weapon.AmmoType "MAGnum"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2
	Tag "M29 Magnum"
	Decal "BulletChip"
	states
	{
		Select:
			RVLS A 1 A_Raise
			TNT1 AAAAAAAAAAAAAAAAAAAAA 0 A_Raise
			Goto Ready
		Ready:
			RVLS A 1 A_PlaySound("Weapons/M29SWSH", 0)
			RVLS BCDEFGHIJ 1
			RVLS K 1 A_PlaySound("Weapons/M29Slct", 0)
			Goto Ready2
		Deselect:
			"----" A 1 A_JumpIfHealthLower(1, "DeselectDeath")
			
			RVLS K 1 A_PlaySound("Weapons/M29SWSH", 0)
			RVLS JIHGFEDCBA 1
			TNT1 A 1 A_Lower
			TNT1 AAAAAA 0 A_Lower
			Wait
		DeselectDeath:
			RVLS A 1 A_Lower
			Wait
		Ready2:
			RVLV A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			RVLV A 1 A_JumpIfInventory("MAGnum", 1, 1)
			Goto Reload
			
			RVLF A 2 bright A_FireBullets(2.5,2.5,-1, 30,"ngBulletPuff")
			TNT1 A 0 A_FireCustomMissile("BulletTrail", FRANDOM(-1, 1), 0, 0, 0, 0, FRANDOM(-2, 2))
			TNT1 A 0 A_PlaySound("Weapons/M29Fire")
			TNT1 A 0 A_TakeInventory("MAGnum", 1)
			TNT1 AAAA 0 A_FireCustomMissile("SmonkSpawner", 0, 0, 12, 2)
			TNT1 A 0 A_SetPitch(pitch-.8)
			RVLF BCDE 2
			RVLV A 2
			TNT1 A 0 A_Refire
			Goto Ready2
		Reload:
			RVLV A 1 A_JumpIfInventory("MAGnum", 0, "Ready2")
			
			TNT1 A 0 A_PlaySound("Weapons/M29R", 0)
			RVLR ABCDEFGHIJKLMNOPQRSTUV 2
			TNT1 A 0 ACS_NamedExecuteAlways("ReloadWeapon", 0, 1, 0, 0)
			Goto Ready2
	}
}
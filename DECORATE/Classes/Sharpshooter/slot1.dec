ACTOR AlwaysGibPuff 
{
  +NOBLOCKMAP
  +NOGRAVITY
  +ALLOWPARTICLES
  +RANDOMIZE
  +EXTREMEDEATH
  RenderStyle Translucent
  Alpha 0.5
  VSpeed 1
  Mass 5
  States
  {
  Spawn:
    PUFF A 4 Bright
    PUFF B 4
    // Intentional fall-through
  Melee:
    PUFF CD 4
    Stop
  }
}
Actor RhinoAmmo : Ammo
{
	Inventory.RestrictedTo "SharpshooterPlayer"
	Inventory.MaxAmount 80
	Ammo.BackpackMaxAmount 160
	+IGNORESKILL
}
Actor RhinoMag : Ammo
{
	Inventory.MaxAmount 3
	+IGNORESKILL
}

Actor Rhino : Weapon
{
	Obituary "%o was blown into next week by %k's Rhino."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 2
	Weapon.AmmoType "RhinoAmmo"
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "RhinoMag"
	Weapon.SlotNumber 3
	Decal BulletChip
	Tag "Rhino"
	Inventory.RestrictedTo "SharpshooterPlayer"
	Inventory.PickupMessage "You picked up the Rhino."
	+AMMO_CHECKBOTH
	+NOAUTOFIRE
	
	states
	{
	Ready:
		RHNO A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Select:	
		RHNO A 0 A_Raise
		RHNO A 1 Offset(16, 78) A_PlaySound("Weapons/RHINOPIK")
		RHNO A 1 Offset(16, 52)
		RHNO A 1 Offset(14, 40)
		RHNO A 1 Offset(10, 34)
		RHNO A 1 Offset(4, 32)
		RHNO A 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
		
		RHNO A 1 Offset(4, 32)
		RHNO A 1 Offset(10, 34)
		RHNO A 1 Offset(14, 40)
		RHNO A 1 Offset(16, 52)
		RHNO A 1 Offset(16, 78)
		RHNO A 0 A_Lower
		Wait
	DeselectDeath:
		RHNO A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("RhinoMag", 1, 1)
		Goto Reload
		
		TNT1 A 0 A_TakeInventory("RhinoMag", 1)
		TNT1 A 0 A_PlaySound("Weapons/RHINOFIR", 1, .5)
		RHNF B 1 bright A_FireBullets(5, 5, 6, 25, "AlwaysgibPuff", FBF_NORANDOM)
		TNT1 A 0 A_Recoil(5)
		TNT1 A 0 A_SetPitch(pitch-10)
		RHNF C 2 bright
		RHNF C 2 bright Offset(0, 2)
		RHNF C 2 bright Offset(0, -1)
		RHNF D 2
		RHNF D 2 Offset(0, -1)
		RHNF D 2 Offset(0, 10)
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("RhinoMag", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("RhinoAmmo", 1, 1)
		Goto Ready
		
		RHNO A 2 Offset(9, -9) A_PlaySound("Weapons/RHINOSEL")
		RHNO A 2 Offset(18, -18)
		RHNO B 2 
		RHNO B 2 Offset(20, -20)
		RHNO B 2 Offset(22, -22)
		RHNR A 2
		RHNR B 2
		RHNR C 2 A_PlaySound("Weapons/RHINOR2")
		RHNR D 2 Offset(25, 0) 
		RHNR E 2 Offset(31, 0) A_PlaySound("Weapons/RHINOR3")
		RHNR F 2 Offset(34, 0)
		RHNR G 2 Offset(31, 0)
		RHNO B 2 Offset(25, 0)
		RHNO B 2 Offset(9, 0)
		RHNO B 2 Offset(-1, -22)
		RHNO B 2 Offset(-9, -18)
		RHNO A 2 Offset(9, 20)
		RHNO A 2 Offset(1, 24)
		Goto ReloadLoop
	ReloadLoop:
		TNT1 A 0 A_JumpIfInventory("RhinoMag", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("RhinoAmmo", 1, 1)
		Goto Ready
		
		TNT1 A 0 A_GiveInventory("RhinoMag", 1)
		TNT1 A 0 A_TakeInventory("RhinoAmmo", 1)
		Loop
	Flash:
		TNT1 A 1 bright
		Stop
	}
}
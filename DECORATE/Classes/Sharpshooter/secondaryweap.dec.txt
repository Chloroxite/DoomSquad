Actor FRSwitchingMode : Inventory
{
	Inventory.MaxAmount 1
}

Actor FRAmmo : Ammo
{
	Inventory.MaxAmount 40
	Inventory.RestrictedTo "SharpshooterPlayer"
	Ammo.BackpackMaxAmount 80
	+IGNORESKILL
}

Actor FSAmmo : Ammo
{
	Inventory.MaxAmount 25
	Inventory.RestrictedTo "SharpshooterPlayer"
	Ammo.BackpackMaxAmount 50
	+IGNORESKILL
}

Actor FRBolt
{
	Damage 30
    Radius 2
    Height 2
    Speed 100
    Projectile 
    Obituary "%o was shredded by %k's Jackhammer Sweeper rounds."
    Renderstyle "Normal"
    Species "Player"
    Scale 0.5
	Decal "BulletChip"
	Gravity 0.25
    +FORCERADIUSDMG
	+BLOODSPLATTER
    +THRUSPECIES
	+RIPPER
	states
	{
	Spawn:
		BOLT A 1
		Loop
	Death:
		BOLT A 100
		Stop
	}
}

Actor FSBolt
{
	Damage 2
    Radius 2
    Height 2
    Speed 100
    Projectile 
    Obituary "%o was shredded by %k's Jackhammer Sweeper rounds."
    Renderstyle "Normal"
    Species "Player"
    Scale 0.5
	Decal "BulletChip"
	Gravity 0.25
    +FORCERADIUSDMG
	+BLOODSPLATTER
    +THRUSPECIES
	-NOGRAVITY
	states
	{
	Spawn:
		SPBT A 1 bright
		Loop
	Death:
		TNT1 A 0 A_PlaySound("Misc/EXPLOSION")
		MISL B 4 bright A_Explode(200, 225)
		MISL C 3 bright
		MISL D 2 bright
		Stop
	}
}

Actor FleshRender : Weapon
{
	Obituary "%o was rended by %k's Fleshrender."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "FRAmmo"
	Weapon.SlotNumber 4
	Decal BulletChip
	Tag "Fleshrender"
	Inventory.RestrictedTo "SharpshooterPlayer"
	Inventory.PickupMessage "Hol up."
	+AMMO_CHECKBOTH
	+NOAUTOFIRE
	
	states
	{
	Select:
		TNT1 A 0 A_JumpIfInventory("FRSwitchingMode", 1, "SelectMode")
		
		XBWS A 1 Offset(48, 76) A_PlaySound("Weapons/XBSel")
		XBWS A 1 Offset(32, 56)
		XBWS A 1 Offset(16, 36)
		XBWS A 1 Offset(5, 35)
		XBWS A 1 Offset(4, 33)
		Goto Ready
	SelectMode:
		TNT1 A 0 A_TakeInventory("FRSwitchingMode", 1)
		Goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfInventory("FRSwitchingMode", 1, "DeselectMode")
		TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
		
		XBWS A 1 Offset(4, 33)
		XBWS A 1 Offset(5, 35)
		XBWS A 1 Offset(16, 36)
		XBWS A 1 Offset(32, 56)
		XBWS A 1 Offset(48, 76)
		XBWS A 0 A_Lower
		Wait
	DeselectMode:
		XBWS A 0 A_Lower
		Wait
	DeselectDeath:
		XBWS A 1 A_Lower
		Wait
	Ready: 
		XBWS A 1 A_WeaponReady
		Loop
	AltFire:
		TNT1 A 0 A_JumpIfInventory("FleshSploder", 1, 1)
		Goto ready
		
		TNT1 A 0 A_JumpIfInventory("FSAmmo", 1, 1)
		Goto Ready
		
		TNT1 A 0 A_GiveInventory("FRSwitchingMode", 1)
		TNT1 A 0 A_SelectWeapon("FleshSploder")
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("FRAmmo", 1, 1)
		Goto Ready
		
		XBWS D 1 A_FireCustomMissile("FRBolt", 0, 0, -2)
		TNT1 A 0 A_PlaySound("Weapons/XBFir", 1)
		TNT1 A 0 A_TakeInventory("FRAmmo", 1)
		XBWS D 1 Offset(0, 35)
		XBWS D 1 Offset(0, 32)
		XBWS D 15 
		XBWS D 2 Offset(-2, 30)
		XBWS D 2 Offset(-4, 32)
		XBWR A 2 Offset(4, 28)
		XBWR A 2 Offset(2, 30)
		XBWR A 2
		XBWR B 2
		XBWR C 2
		XBWR D 2
		XBWR E 2
		XBWR F 2
		XBWR G 2 A_PlaySound("Weapons/XBWR")
		XBWR H 2 
		XBWR H 2 Offset(2, 30)
		XBWR H 2 Offset(4, 28)
		XBWS A 2 Offset(-4, 32)
		XBWS A 2 Offset(-2, 30)
		Goto Ready
	}
}

Actor FleshSploder : FleshRender
{
	Obituary "%o had their flesh 'sploded by %k's Fleshsploder bolts"
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.SlotNumber ""
	Weapon.AmmoType "FSAmmo"
	
	states
	{
	Select:
		XBWS A 0 Offset(0, 32) A_PlaySound("Weapons/M4A1R2")
		Goto ReadySprite
	Ready:
		XBWS C 1 A_WeaponReady
		Loop
	ReadySprite:
		XBWS A 2
		XBWS B 4
		XBWS C 6
		Goto Ready
	DeselectMode:
		XBWS C 4 A_PlaySound("Weapons/M4A1R2")
		XBWS B 2
		XBWS A 2
		TNT1 A 0 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("FSAmmo", 1, 1)
		Goto AltFire
		
		XBWS D 1 A_FireCustomMissile("FSBolt", 0, 0, -3)
		TNT1 A 0 A_PlaySound("Weapons/XBFir", 1)
		TNT1 A 0 A_TakeInventory("FSAmmo", 1)
		XBWS D 1 Offset(0, 35)
		XBWS D 1 Offset(0, 32)
		XBWS D 15 
		XBWS D 2 Offset(-2, 30)
		XBWS D 2 Offset(-4, 32)
		XBWR A 2 Offset(4, 28)
		XBWR A 2 Offset(2, 30)
		XBWR A 2
		XBWR B 2
		XBWR C 2
		XBWR D 2
		XBWR E 2
		XBWR F 2
		XBWR G 2 A_PlaySound("Weapons/XBWR")
		XBWR H 2 
		XBWR H 2 Offset(2, 30)
		XBWR H 2 Offset(4, 28)
		XBWS A 2 Offset(-4, 32)
		XBWS A 2 Offset(-2, 30)
		Goto ReadySprite
	AltFire:
		TNT1 A 0 A_GiveInventory("FRSwitchingMode", 1)
		TNT1 A 0 A_SelectWeapon("FleshRender")
		Goto Ready
	
	}
}


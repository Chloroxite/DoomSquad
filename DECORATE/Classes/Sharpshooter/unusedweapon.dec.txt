//When this weapon was made, the sharpshooter was originally going to be called the specialist, with an emphasis on high single target dps through sniping.
//What ended up happening is I found this weapon stupidly boring to use, nothing about it felt good. So I rebranded the specialist as the sharpshooter, and gave
//him a god damn hand cannon straight out of thor's asshole. A much needed improvement. This weapon sits here unused. You can fuck with it if you want.

Actor DMRMag : Ammo
{
	Inventory.MaxAmount 12
	+IGNORESKILL
}

Actor LRDMR : Weapon
{
	Obituary "%o was picked off by %k's LRDMR."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 2
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "DMRMag"
	Weapon.SlotNumber 3
	Decal BulletChip
	Tag "LRDMR"
	Inventory.RestrictedTo "SpecialistPlayer"
	Inventory.PickupMessage "You picked up the LRDMR."
	+AMMO_CHECKBOTH
	+NOAUTOFIRE
	
	states
	{
	Ready:
		HRST A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		HRST A 1 
		HRST A 1 Offset(20, 0)
		HRST A 1 Offset(36, 0)
		HRPK A 1 Offset(70, 30)
		HRPK A 1 Offset(120, 50)
		HRPK A 1 Offset(140, 80)
		HRPK B 1 Offset(160, 120)
		HRPK B 1 Offset(180, 170)
		HRPK B 1 Offset(200, 230)
		HRPK B 1 Offset(220, 300)
		HRPK B 1 Offset(240, 330)
		HRPK B 0 A_Lower
		Wait
	Select:
		HRPK B 0 A_Raise
		HRPK B 1 Offset(240, 330) A_PlaySound("Weapons/LRDMRS")
		HRPK B 1 Offset(220, 300)
		HRPK B 1 Offset(200, 230)
		HRPK A 1 Offset(180, 170)
		HRPK A 1 Offset(160, 120)
		HRPK A 1 Offset(140, 80)
		HRPK A 1 Offset(120, 50)
		HRPK A 1 Offset(70, 30)
		HRST A 1 Offset(36, 30)
		HRST A 1 Offset(20, 30)
		Loop
	Fire:
		HRFR A 0 A_JumpIfInventory("DMRMag", 1, 1)
		Goto Reload
		
		HRFR A 1
		HRFR A 0 A_Recoil(1)		
		HRFR A 1 bright A_FireBullets (1,1,1,25,"BulletPuff", FBF_NORANDOM)
		HRFR A 0 A_SetPitch(pitch-2)
		HRFR A 0 A_ZoomFactor(0.98,ZOOM_NOSCALETURNING)
		HRFR A 0 A_PlaySound("Weapons/LRDMRF", 1, 0.5)
		HRFR B 1 Radius_Quake (1,1,0,1,0)
		HRST A 0 A_TakeInventory("DMRMag", 1)
		HRST A 0 A_ZoomFactor(1.0)
		HRST A 2
		Goto Ready
	Reload:
		HRST A 0 A_JumpIfInventory("DMRMag", 0, "Reset")
		HRST A 0 A_JumpIfInventory("Clip", 1, 1)
		Goto Reset
		
		HRST A 1
		HRST A 2 Offset(0, 30)
		HRST A 2 Offset(10, 24)
		HRST A 2 Offset(10, 26)
		HRST A 2 Offset(20, 38)
		HRST A 2 Offset(20, 39)
		HRST A 18 Offset(20, 40) A_PlaySound("Weapons/M4A1R1")
		HRPK A 2 Offset(10, 38)
		HRPK A 2 Offset(15, 40)
		HRPK A 2 Offset(15, 41)
		HRPK A 8 Offset(15, 20) A_PlaySound("Weapons/M4A1R2")
		HRPK A 2 Offset(15, 22)
		HRPK A 2 Offset(15, 24)
		HRPK A 18 Offset(15, 18) A_PlaySound("Weapons/M4A1R3")
		HRPK A 4 Offset(15, 18)
		HRPK A 2 Offset(10, 38)
		HRST A 2 Offset(2, 50)
		HRST A 2 Offset(2, 39)
		HRST A 2 Offset(2, 32)
		Goto ReloadLoop
	ReloadLoop:
		HORN A 0 A_JumpIfInventory("DMRMag", 0, "Ready")
	
		HORN A 0 A_JumpIfInventory("Clip", 1, 1)
		Goto Ready
	
		HORN A 0 A_GiveInventory("DMRMag", 1)
		HORN A 0 A_TakeInventory("Clip", 1)
		Loop
	Reset:
		M4A1 A 0
		Goto Ready
	Flash:
		TNT1 A 2 bright
		Stop
	Spawn:
		M4A1 A -1
		Stop
	}
}
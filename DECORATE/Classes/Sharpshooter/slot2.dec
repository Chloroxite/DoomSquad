Actor RifleMag : Ammo
{
	Inventory.RestrictedTo "SharpshooterPlayer"
	Inventory.MaxAmount 16
	+IGNORESKILL
}

Actor Rifle : Weapon
{
	Obituary "%o found themselves on the wrong end of %k's rifle."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 0
	Weapon.AmmoType "Clip"
	Weapon.AmmoType2 "RifleMag"
	Weapon.SlotNumber 4
	Decal BulletChip
	Tag "Rifle"
	Inventory.RestrictedTo "SharpshooterPlayer"
	Inventory.PickupMessage "Hol up."
	+AMMO_CHECKBOTH
	states
	{
	Select:
		LVAE D 1 Offset(0, 50) A_PlaySound("Weapons/RIFRL1", 1)
		LVAE CBA 1
		Goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
		
		LVAE ABCD 1
		LVAE A 0 A_Lower
		Wait
	DeselectDeath:
		LVAS A 1 A_Lower
		Wait
	Ready:
		LVAS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Fire:
		LVAF A 0 A_JumpIfInventory("RifleMag", 1, 1)
		Goto Reload
		
		LVAF A 0 A_FireBullets(1, 1, 1, 50)
		LVAF A 0 A_TakeInventory("RifleMag", 1)
		LVAF A 1 BRIGHT A_PlaySound("Weapons/RIFFIR", 1, 0.5)
		TNT1 A 0 A_FireCustomMissile("BulletTrail", FRANDOM(-1, 1), 0, 0, 0, 0, FRANDOM(-1, 1))
		TNT1 A 0 A_SetPitch(pitch-3)
		LVAF B 1 BRIGHT
		LVAF C 1
		LVAF D 2 A_PlaySound("Weapons/RIFRL1")
		LVAF E 2
		LVAF F 2 A_PlaySound("Weapons/RIFRl2")
		LVAF G 2
		LVAS A 2
		Goto Ready
	Reload:
		TNT1 A 0 A_JumpIfInventory("RifleMag", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
		Goto Ready
		
		LVAS ABCDE 1
		Goto "AnimateReload"
	AnimateReload:
		TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
		Goto FinishReload
		
		LVAR ABCDEF 1
		LVAR G 1 A_PlaySound("Weapons/RIFRL3")
		TNT1 A 0 A_TakeInventory("Clip", 1)
		TNT1 A 0 A_GiveInventory("RifleMag", 1)
		LVAR HIJLM 1
		
		TNT1 A 0 A_JumpIfInventory("RifleMag", 0, "FinishReload")
		Loop
	FinishReload:
		LVAS EDCBA 1
		Goto Ready
	}
}
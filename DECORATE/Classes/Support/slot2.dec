Actor SMGMag : Ammo
{
	Inventory.MaxAmount 24
	+IGNORESKILL
}

Actor Badger : Weapon
{
	Obituary "%o was widdled down by %k's Badger."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 4
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "SMGMag"
	Weapon.SlotNumber 4
	Decal BulletChip
	Tag "Badger"
	Inventory.RestrictedTo "SupportPlayer"
	Inventory.PickupMessage "You somehow picked up a badger straight off the ground..."
	
	+WEAPON.AMMO_CHECKBOTH
	
	States
	{
		Select:
			MP5R A 0 A_Raise
			MP5R A 1 Offset(240, 330) A_PlaySound("Weapons/SMGPK")
			MP5R A 1 Offset(220, 300)
			MP5R A 1 Offset(200, 290)
			MP5R A 1 Offset(180, 230)
			MP5R A 1 Offset(160, 180)
			MP5R A 1 Offset(140, 140)
			MP5R A 1 Offset(120, 110)
			MP5R A 1 Offset(70, 90)
			MP5K A 1 Offset(36, 30)
			MP5K A 1 Offset(20, 30)
			Goto Ready
		Deselect:
			TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
			
			MP5K A 1
			MP5K A 1 Offset(20, 0)
			MP5K A 1 Offset(36, 0)
			MP5R A 1 Offset(70, 90)
			MP5R A 1 Offset(120, 110)
			MP5R A 1 Offset(140, 140)
			MP5R A 1 Offset(160, 180)
			MP5R A 1 Offset(180, 230)
			MP5R A 1 Offset(200, 290)
			MP5R A 1 Offset(220, 300)
			MP5R A 1 Offset(240, 330)
			MP5R A 0 A_Lower
			Wait
		DeselectDeath:
			MP5K A 1 A_Lower
			Wait
		Ready:
			//MP5K A 0 A_GiveInventory("SMGHealerAmmo", 1)
			MP5K A 2 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("SMGMag", 1, 1)
			Goto Reload
			
			MP5K A 1 A_TakeInventory("SMGMag", 1)
			MP5F A 1 bright A_FireBullets(5, 4, -1, 16, "BulletPuff", FBF_NORANDOM)
			MP5K A 0 A_SetPitch(pitch-1)
			MP5F A 0 A_PlaySound("Weapons/SMGFIR", 1, 0.8)
			MP5F B 1
			MP5F B 0 A_ReFire
			Goto Ready
		Reload:
			TNT1 A 0 A_JumpIfInventory("SMGMag", 0, "Reset")
			TNT1 A 0 A_JumpIfInventory("Clip", 1, 1)
			Goto Reset
			
			MP5K A 2
			MP5R A 2
			MP5R B 2 A_PlaySound("Weapons/SMGR1")
			MP5R C 2 
			MP5R D 2
			MP5R E 2 Offset(-32, 0)
			MP5R E 2 Offset(-40, 0)
			MP5R E 4
			MP5R F 2
			MP5R G 2 Offset(-40, 0) A_PlaySound("Weapons/SMGR2")
			MP5R G 2 Offset(-30, 0)
			MP5R G 2 Offset(-28, 0)
			MP5R G 2 Offset(-30, 0)
			MP5R G 2 Offset(-40, 0)
			MP5R H 2
			Goto ReloadLoop
		ReloadLoop:
			HORN A 0 A_JumpIfInventory("SMGMag", 0, "Ready")
		
			HORN A 0 A_JumpIfInventory("Clip", 1, 1)
			Goto Ready
		
			HORN A 0 A_GiveInventory("SMGMag", 1)
			HORN A 0 A_TakeInventory("Clip", 1)
			Loop
		Reset:
			TNT1 A 0
			Goto Ready
		Spawn:
			//This weapon sprite should never show up in world
			Stop 
	}
}
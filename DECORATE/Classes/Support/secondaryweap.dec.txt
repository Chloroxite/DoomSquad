Actor RLSwitchingModes : Inventory
{
	Inventory.MaxAmount 1
}

Actor RLMag : Ammo
{
	+IGNORESKILL
	Inventory.RestrictedTo "SupportPlayer"
	Inventory.MaxAmount 6
}
Actor FlakMag : Ammo
{
	+IGNORESKILL
	Inventory.RestrictedTo "SupportPlayer"
	Inventory.MaxAmount 4
}

Actor Rockets : Ammo
{
	+IGNORESKILL
	Inventory.RestrictedTo "SupportPlayer"
	Inventory.MaxAmount 30
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 60
	Inventory.Icon "ROCKA0"
}
Actor Flak : Ammo
{
	+IGNORESKILL
	Inventory.RestrictedTo "SupportPlayer"
	Inventory.MaxAmount 20
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 40
	Inventory.Icon "FLAKA0"
}

Actor Pulverizer : weapon
{
	Obituary "%o was made into paste by %k's Pulverizer."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 2
	Weapon.AmmoType "Rockets"
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "RLMag"
	Weapon.SlotNumber 4
	Decal BulletChip
	Tag "Pulverizer"
	Inventory.RestrictedTo "SupportPlayer"
	Inventory.PickupMessage "You picked up the Pulverizer."
	
	+WEAPON.AMMO_CHECKBOTH
	+WEAPON.NOAUTOFIRE
	
	states
	{
	Ready:
		RLST A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		loop
	Select:
		TNT1 A 0 A_JumpIfInventory("RLSwitchingModes", 1, "SelectMode")
		
		RLST A 0 A_Raise
		RLST A 1 Offset(16, 78) A_PlaySound("Weapons/RLREADY")
		RLST A 1 Offset(16, 52)
		RLST A 1 Offset(14, 40)
		RLST A 1 Offset(10, 34)
		RLST A 1 Offset(4, 32)
		Goto Ready
	SelectMode:
		RLST A 1 Offset(0, 40)
		RLST A 15 A_PlaySound("Weapons/RLALTRDY", 1)
		TNT1 A 0 A_TakeInventory("RLSwitchingModes", 1)
		Goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
		TNT1 A 0 A_JumpIfInventory("RLSwitchingModes", 1, "DeselectMode")
		
		RLST A 1 Offset(4, 32)
		RLST A 1 Offset(10, 34)
		RLST A 1 Offset(14, 40)
		RLST A 1 Offset(16, 52)
		RLST A 1 Offset(16, 78)
		RLST A 1 Offset(16, 108)
		RLST A 1 A_Lower
		Wait
	DeselectDeath:
		TNT1 A 0 A_TakeInventory("RLSwitchingModes", 1)
		RLST A 1 A_Lower
		Stop
	DeselectMode:
		TNT1 A 0 A_Lower
		Wait
	Fire:
		RLST A 0 A_JumpIfInventory("RLMag", 1, 1)
		Goto Reload
		
		RLST A 0 A_Recoil(10)
		RLST A 0 A_PlaySound("Weapons/RLFIR")
		RLST A 0 A_TakeInventory("RLMag", 1)
		RLST B 1 A_FireCustomMissile("RLRocket")
		RLST C 1 A_SetPitch(pitch-2)
		RLST A 10
		Goto Ready
	Altfire:
		TNT1 A 0 A_JumpIfInventory("FlakMan", 1, 1)
		Goto Ready
		
		TNT1 A 0 A_GiveInventory("RLSwitchingModes", 1)
		TNT1 A 0 A_SelectWeapon("FlakMan")
		Goto Reset
	Reload:
		TNT1 A 0 A_JumpIfInventory("RLMag", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("Rockets", 1, 1)
		Goto Ready
		
		RLRE A 2
		Goto ReloadLoop
	ReloadLoop:
		TNT1 A 0 A_JumpIfInventory("RLMag", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("Rockets", 1, 1)
		Goto Ready
		
		RLRE B 2
		RLRE C 2
		RLRE D 2
		RLRE E 2 A_PlaySound("Weapons/M4A1GRER", 1)
		RLRE F 2
		RLRE G 2
		RLRE H 2
		RLRE I 2
		TNT1 A 0 A_TakeInventory("Rockets", 1)
		TNT1 A 0 A_GiveInventory("RLMag", 1)
		Goto ReloadLoop
	Reset:
		TNT1 A 0
		Goto Ready
	}
}

Actor FlakMan : Pulverizer
{
	Obituary "%o was filled with flak by %k's Pulverizer."
	Weapon.AmmoType "Flak"
	Weapon.AmmoType2 "FlakMag"
	Weapon.SlotNumber ""
	States
	{
	Select:
		RLST A 1 Offset(0, 40)
		RLST A 15 A_PlaySound("Weapons/RLALTRDY", 1)
		TNT1 A 0 A_TakeInventory("RLSwitchingModes", 1)
		Goto Ready
	Fire:
		RLST A 0 A_JumpIfInventory("FlakMag", 1, 1)
		Goto Reload
		
		RLST A 0 A_Recoil(10)
		RLST A 0 A_PlaySound("Weapons/RLFIR")
		RLST A 0 A_TakeInventory("FlakMag", 1)
		RLST B 1 A_FireCustomMissile("FlakRocket")
		RLST C 1 A_SetPitch(pitch-2)
		RLST A 40
		Goto Ready
	Altfire:
		TNT1 A 0 A_GiveInventory("RLSwitchingModes", 1)
		TNT1 A 0 A_SelectWeapon("Pulverizer")
		Goto Reset
	Reload:
		TNT1 A 0 A_JumpIfInventory("FlakMag", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("Flak", 1, 1)
		Goto Ready
		
		RLRE A 2
		Goto ReloadLoop
	ReloadLoop:
		TNT1 A 0 A_JumpIfInventory("FlakMag", 0, "Ready")
		TNT1 A 0 A_JumpIfInventory("Flak", 1, 1)
		Goto Ready
		
		RLRE B 2
		RLRE C 2
		RLRE D 2
		RLRE E 2 A_PlaySound("Weapons/M4A1GRER", 1)
		RLRE F 2
		RLRE G 2
		RLRE H 2
		RLRE I 2
		TNT1 A 0 A_TakeInventory("Flak", 1)
		TNT1 A 0 A_GiveInventory("FlakMag", 1)
		Goto ReloadLoop
	}
}

Actor RLRocket
{
	Damage 1
    Radius 2
    Height 2
    Speed 80
    Projectile 
    Obituary "%o was turned into paste by %k's Pulverizer Rocket."
    Renderstyle "Normal"
    Species "Player"
    Scale 0.5
	DeathSound "Misc/EXPLOSION2"
    Decal DoomImpScorch
    +FORCERADIUSDMG
	+BLOODSPLATTER
    +THRUSPECIES
	states
	{
	Spawn:
		MISL A 1
		Loop
	Death:
		MISL B 0 A_Explode(180, 150)
		MISL B 8 bright A_NoGravity
		MISL C 6 bright
		MISL D 4 bright
		Stop
	}
}
Actor FlakRocket
{
	Damage 1
    Radius 2
    Height 2
    Speed 80
    Projectile 
    Obituary "%o was turned into paste by %k's Pulverizer Flak."
    Renderstyle "Normal"
    Species "Player"
    Scale 0.5
    Decal DoomImpScorch
    +FORCERADIUSDMG
	+BLOODSPLATTER
    +THRUSPECIES
	+EXTREMEDEATH
	states
	{
	Spawn:
		MISL A 1
		Loop
	Death:
		MISL B 0 A_Explode(60, 150)
		MISL B 0 A_PlaySound("Misc/EXPLOSION2", 0 | 4096)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile("FlakShard", 32, 0, random(0, 360), CMF_AIMDIRECTION, random(-180, 180))
		MISL B 8 bright A_NoGravity
		MISL C 6 bright
		MISL D 4 bright
		Stop
	}
}

Actor FlakShard
{
	Damage 1
    Radius 1
    Height 1
    Speed 10
    Projectile
    Obituary "%o was flaked by %k's Pulverizer Flak"
    Renderstyle "Normal"
    Species "Player"
    Scale 0.5
    +FORCERADIUSDMG
	+BLOODSPLATTER
    +THRUSPECIES
	+EXTREMEDEATH
	states
	{
	Spawn:
		TRAI A 1 bright
		TNT1 A 6 A_Jump(64, 1)
		TRAI A 0 A_Die
		TNT1 A 2 A_Jump(64, 1)
		TRAI A 0 A_Die
		TNT1 A 2 A_Jump(64, 1)
		TRAI A 0 A_Die
		TNT1 A 2 A_Jump(64, 1)
		TRAI A 0 A_Die
		TNT1 A 2 A_Jump(64, 1)
		TRAI A 0 A_Die
		TNT1 A 2 A_Jump(64, 1)
		TRAI A 0 A_Die
	Death:
		MISL B 0 A_Stop
		MISL B 0 A_PlaySound("Misc/EXPLOSION", 0 | 4096)
		MISL B 0 A_Explode(5, 250)
		MISL B 8 bright A_NoGravity
		MISL C 6 bright
		MISL D 4 bright
		Stop
	}
}
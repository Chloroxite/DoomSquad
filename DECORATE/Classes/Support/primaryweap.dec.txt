actor SGSwitchingModes : Inventory
{
	+IGNORESKILL
	Inventory.MaxAmount 1
}
actor Slugs : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 50
	Inventory.Icon "SLUGA0"
}
actor SGMag : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 6
}
actor SG12 : Weapon
{
	Obituary "%o was given facial deconstruction surgery by %k's shotgun."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 2
	Weapon.AmmoType "Shell"
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "SGMag"
	Weapon.SlotNumber 3
	Decal BulletChip
	Tag "SG12"
	Inventory.RestrictedTo "SupportPlayer"
	Inventory.PickupMessage "You picked up the SG12."
	
	+WEAPON.AMMO_CHECKBOTH
	
	states
	{
	Ready:
		DNSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfInventory("SGSwitchingModes", 0, "DeselectMode")
		
		TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
		
		DNSG F 0
		DNSG F 1 Offset(-3, 35)
		DNSG F 1 Offset(-20, 35)
		DNSG F 1 Offset(-42, 35)
		DNSG J 1 Offset(-30, 46)
		DNSG J 1 Offset(-60, 60)
		DNSG J 1 Offset(-90, 76)
		DNSG J 1 Offset(-130, 100)
		TNT1 A 5
		TNT1 A 0 A_Lower
		Wait
	DeselectMode:
		TNT1 A 0 A_Lower
		Wait
	DeselectDeath:
		TNT1 A 0 A_TakeInventory("SGSwitchingModes", 1)
		DNSG A 1 A_Lower
		Wait
	Select:
		TNT1 A 0 A_JumpIfInventory("SGSwitchingModes", 1, "SelectMode")
		
		TNT1 A 0 A_Raise
		TNT1 A 0 A_PlaySound("Weapons/SGPICKUP")
		DNSG J 1 Offset(-130, 100)
		DNSG J 1 Offset(-90, 76)
		DNSG J 1 Offset(-60, 60)
		DNSG J 1 Offset(-30, 46)
		DNSG F 1 Offset(-42, 35)
		DNSG F 1 Offset(-20, 35)
		DNSG F 1 Offset(-3, 35) A_TakeInventory("SGSwitchingModes", 1)
		Goto Ready
	SelectMode:
		DNSG A 2 Offset(-3, 35) A_PlaySound("Weapons/SGPUMP1")
		DNSG A 2 Offset(-20, 36)
		DNSG F 2 Offset(-22, 37)A_PlaySound("Weapons/SGPUMP2")
		DNSG F 2 Offset(-22, 38)
		DNSG F 2 Offset(-22, 37)
		DNSG F 2 Offset(-20, 36)
		DNSG F 2 Offset(-3, 35)
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("SGMag", 1, 1)
		Goto Reload
		
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_ZoomFactor(0.98,ZOOM_NOSCALETURNING)
		TNT1 A 0 A_GunFlash
		DNSG B 1 A_FireBullets (4, 3, 7, 10, "BulletPuff", FBF_NORANDOM)
		TNT1 A 0 A_SetPitch(pitch-3)
		DNSG B 0 A_PlaySound("Weapons/SGFIR", 1, 0.5)
		DNSG B 0 A_TakeInventory(SGMag, 1)
		DNSG C 1 Bright A_ZoomFactor(1.0)
		DNSG DE 1 Bright 
		DNSG MNO 1
		DNSG A 3 A_CheckReload
		DNSG FGH 1
		DNSG I 2 
		DNSG J 3 A_PlaySound("Weapons/SGPUMP")
		DNSG KLKJ 1
		DNSG I 3 A_PlaySound("Weapons/SGPUMP2")
		DNSG HGF 1
		DNSG A 0
		DNSG A 8 
		Goto Ready
	AltFire:
		TNT1 A 0 A_GiveInventory("SGSwitchingModes", 1)
		TNT1 A 0 A_SelectWeapon("SG12SlugMode")
		Goto Reset
	Reload:
		DNSG A 0 A_JumpIfInventory("Shell", 1, 1)
		Goto Reset
		
		DNSG A 0 A_JumpIfInventory("SGMag", 0, "Reset")
		DNSG A 0 A_PlaySound("Weapons/SGPICKUP")
		DNSG FGHJ 1 
		DNSG A 0 A_JumpIfInventory("SGMag", 2, "AnimateReload")
		DNSG K 1 A_PlaySound("Weapons/SGPUMP")
		DNSG L 10
		DNSG L 2 Offset(0,-15)
		DNSG L 2 Offset(0, -28)
		DNSG L 0 A_TakeInventory("Shell", 1)
		DNSG L 0 A_GiveInventory("SGMag", 1)
		DNSG L 2 Offset(0, -30) A_PlaySound("Weapons/SGRELOAD")
		DNSG L 2 Offset(0, -28)
		DNSG L 2 Offset(0, -15)
		DNSG L 10 Offset(0, 1)
		DNSG J 4 A_PlaySound("Weapons/SGPUMP2")
		Goto "AnimateReload"
	AnimateReload:
		DNSG J 0 A_JumpIfInventory("Shell", 1, 5)
		DNSG JHGF 1
		Goto Ready
		
		DNSG J 6 Offset(0, 20)
		DNSG J 2 Offset(0,8)
		DNSG J 0 A_TakeInventory("Shell", 1)
		DNSG J 0 A_GiveInventory("SGMag", 1)
		DNSG J 2 Offset(0, -10) A_PlaySound("Weapons/SGRELOAD")
		DNSG J 2 Offset(0, 7)
		DNSG J 2 Offset(0, 9) 
		DNSG J 2 
		DNSG J 0 A_JumpIfInventory("SGMag", 0, 1) 
		Loop
		
		DNSG JHGF 1
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		Goto LightDone
	Reset:
		TNT1 A 0
		Goto Ready
	Spawn:
		DNSP A -1
		Stop
	}
}

Actor SG12SlugMode : SG12
{
	Weapon.SlotNumber ""
	Weapon.AmmoType2 ""
	Weapon.AmmoType1 "Slugs"
	Tag "SG12 Slug Mode"
	states
	{
	Ready:
		DNSG A 1 A_WeaponReady
		Loop
	Select:
		TNT1 A 0 A_Raise
		DNSG A 2 Offset(-3, 35) A_PlaySound("Weapons/SGPUMP1")
		DNSG A 2 Offset(-20, 36)
		DNSG F 2 Offset(-22, 37)A_PlaySound("Weapons/SGPUMP2")
		DNSG F 2 Offset(-22, 38)
		DNSG F 2 Offset(-22, 37)
		DNSG F 2 Offset(-20, 36)
		DNSG F 2 Offset(-3, 35) A_TakeInventory("SGSwitchingModes", 1)
		Goto Ready
	Deselect:
		TNT1 A 0 A_JumpIfInventory("SGSwitchingModes", 1, "DeselectMode")
		
		TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
		
		DNSG F 0
		DNSG F 1 Offset(-3, 35) 
		DNSG F 1 Offset(-20, 35) 
		DNSG F 1 Offset(-42, 35)
		DNSG J 1 Offset(-30, 46)
		DNSG J 1 Offset(-60, 60)
		DNSG J 1 Offset(-90, 76)
		DNSG J 1 Offset(-130, 100)
		TNT1 A 0 A_Lower
		Wait
	DeselectMode:
		TNT1 A 0 A_Lower
		Wait
	DeselectDeath:
		TNT1 A 0 A_TakeInventory("SGSwitchingModes", 1)
		DNSG A 1 A_Lower
		Wait
	Fire:
		TNT1 A 0 A_JumpIfInventory("Slugs", 1, 1)
		Goto Reset
		
		TNT1 A 0 A_Recoil(3)
		TNT1 A 0 A_ZoomFactor(0.96,ZOOM_NOSCALETURNING)
		TNT1 A 0 A_GunFlash
		DNSG B 1 A_FireCustomMissile("HESlugProjectile")
		TNT1 A 0 A_SetPitch(pitch-5)
		DNSG B 0 A_PlaySound("Weapons/SGFIR2", 1, 0.5)
		DNSG B 0 Radius_Quake (2,2,0,1,0)
		DNSG B 0 A_TakeInventory("Slugs", 1)
		DNSG C 1 Bright A_ZoomFactor(1.0)
		DNSG DE 1 Bright 
		DNSG MNO 1
		DNSG A 10 A_CheckReload
		DNSG A 0 A_JumpIfInventory("Slugs", 1, 1)
		Goto AltFire
		
		DNSG FGHJ 1 
		DNSG K 1 A_PlaySound("Weapons/SGPUMP")
		DNSG L 10
		DNSG L 2 Offset(0,-15)
		DNSG L 2 Offset(0, -28)
		DNSG L 2 Offset(0, -30) A_PlaySound("Weapons/SGRELOAD")
		DNSG L 2 Offset(0, -28)
		DNSG L 2 Offset(0, -15)
		DNSG L 10 Offset(0, 1)
		DNSG J 4 A_PlaySound("Weapons/SGPUMP2")
		Goto Ready
	AltFire:
		TNT1 A 0 A_GiveInventory("SGSwitchingModes", 1)
		TNT1 A 0 A_SelectWeapon("SG12")
		Goto Reset
	}
}

Actor HESlugProjectile : FastProjectile
{
	Damage 1
    Radius 1
    Height 1
    Speed 500
    Projectile
    Obituary "%o was turned into mush by %k's Slug Round."
    Renderstyle "Normal"
    Species "Player"
    Scale 0.5
    Decal DoomImpScorch
    +FORCERADIUSDMG
	+BLOODSPLATTER
    +NOTIMEFREEZE
    +THRUSPECIES
	states
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		MISL B 0 A_PlaySound("Misc/EXPLOSION")
		MISL B 0 A_NoGravity
		MISL B 8 A_Explode((160), 80)
		MISL C 6
		MISL D 4
		Stop
	}
}
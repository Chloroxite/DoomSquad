Actor SGEmptyMag : inventory
{
	+IGNORESKILL
	Inventory.MaxAmount 1
}
actor SGMag : Ammo
{
	+IGNORESKILL
	Inventory.MaxAmount 6
}

actor SG12 : Weapon
{
	Obituary "%o was given facial deconstruction surgery by %k's shotgun."
	Weapon.AmmoUse 0
	Weapon.AmmoGive 2
	Weapon.AmmoType "Shell"
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "SGMag"
	Weapon.SlotNumber 3
	Decal BulletChip
	Tag "SG12"
	Inventory.RestrictedTo "SupportPlayer"
	Inventory.PickupMessage "You picked up the SG12."
	
	+WEAPON.AMMO_CHECKBOTH
	
	states
	{
	Ready:
		DNSG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		TNT1 A 0 A_JumpIfHealthLower(1, "DeselectDeath")
		
		DNSG F 0
		DNSG F 1 Offset(-3, 35)
		DNSG F 1 Offset(-20, 35)
		DNSG F 1 Offset(-42, 25)
		DNSG J 1 Offset(-30, 86)
		DNSG J 1 Offset(-60, 100)
		DNSG J 1 Offset(-90, 116)
		DNSG J 1 Offset(-130, 140)
		TNT1 A 5
		TNT1 A 0 A_Lower
		Wait
	DeselectDeath:
		DNSG A 1 A_Lower
		TNT1 A 0 A_TakeInventory("SGEmptyMag", 1)
		Wait
	Select:
		TNT1 A 0 A_Raise
		TNT1 A 0 A_PlaySound("Weapons/SGPICKUP")
		DNSG J 1 Offset(-130, 140)
		DNSG J 1 Offset(-90, 116)
		DNSG J 1 Offset(-60, 100)
		DNSG J 1 Offset(-30, 86)
		DNSG F 1 Offset(-42, 25)
		DNSG F 1 Offset(-20, 35)
		DNSG F 1 Offset(-3, 35) 
		Goto Ready
	Fire:
		TNT1 A 0 A_JumpIfInventory("SGMag", 1, 1)
		Goto Reload
		
		TNT1 A 0 A_Recoil(2)
		TNT1 A 0 A_ZoomFactor(0.98,ZOOM_NOSCALETURNING)
		TNT1 A 0 A_GunFlash
		DNSG B 1 bright A_FireBullets(3, 3, 10, 8, "BulletPuff", FBF_NORANDOM)
		TNT1 AAAAAAAAAA 0 A_FireCustomMissile("BulletTrail", FRANDOM(-1.5, 1.5), 0, 0, 0, 0, FRANDOM(-1.5, 1.5))
		TNT1 A 0 A_SetPitch(pitch-3)
		DNSG B 0 A_PlaySound("Weapons/SGFIR", 1, 0.5)
		DNSG B 0 A_TakeInventory(SGMag, 1)
		DNSG C 1 Bright A_ZoomFactor(1.0)
		DNSG DE 1
		DNSG A 3 A_CheckReload
		DNSG GHIJKLM 1
		DNSG N 1 A_PlaySound("Weapons/SGPUMP3")
		TNT1 A 0 A_SpawnItemEx("SpentShell",Cos(Pitch)*30,-2,25-(Sin(Pitch)*25),Cos(Pitch)*FRandom(2,4),FRandom(3,6),-Sin(Pitch)*FRandom(3,6),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		DNSG OPQR 1
		DNSG Q 3
		DNSG PO 1
		DNSG MLKJIHG 1
		DNSG A 0
		DNSG A 4 
		Goto Ready
	Reload:
		DNSG A 0 A_JumpIfInventory("Shell", 1, 1)
		Goto Reset
		
		DNSG A 0 A_JumpIfInventory("SGMag", 0, "Reset")
		DNSG A 0 A_PlaySound("Weapons/SGPICKUP")
		DNSG GHIJK 1 
		
		TNT1 A 0 A_JumpIfInventory("SGMag", 1, 2)
		TNT1 A 0 A_GiveInventory("SGEmptyMag", 1)
		
		Goto "AnimateReload"
	AnimateReload:
		DNSG J 0 A_JumpIfInventory("Shell", 1, 5)
		DNSG JHGF 1
		Goto Ready
		
		DNSR ABC 1
		DNSG N 0 A_TakeInventory("Shell", 1)
		DNSG N 0 A_GiveInventory("SGMag", 1)
		DNSR D 1 A_PlaySound("Weapons/SGRELOAD")
		DNSR CBA 1
		DNSG N 0 A_JumpIfInventory("SGMag", 0, 1)
		Loop
		
		TNT1 A 0 A_JumpIfInventory("SGEmptyMag", 1, 2)
		TNT1 A 0 A_Jump(256, 13)
		
		DNSG LM 1
		DNSG N 1 A_PlaySound("Weapons/SGPUMP3")
		TNT1 A 0 A_SpawnItemEx("SpentShell",Cos(Pitch)*30,-2,25-(Sin(Pitch)*25),Cos(Pitch)*FRandom(2,4),FRandom(3,6),-Sin(Pitch)*FRandom(3,6),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		DNSG OPQRQPON 1
		
		DNSG MLKJIHG 1
		TNT1 A 0 A_TakeInventory("SGEmptyMag", 1)
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		Goto LightDone
	Reset:
		TNT1 A 0
		Goto Ready
	Spawn:
		DNSP A -1
		Stop
	}
}